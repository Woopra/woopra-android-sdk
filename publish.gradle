apply plugin: 'maven-publish'

ext {
    debugSuffix = '-debug'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    // It's also a good practice for a closed-source SDK to expose some API interfaces
    // with source code, here we assume these has suffix of '.api' in there package name.
    include("**/api/*.java")
    include("**/api/*.kt")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    include("**/api/*.java")
    include("**/api/*.kt")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar) {
    from javadoc
}

afterEvaluate {
    // NOTE:
    // This is a workaround for the known limitation by the Maven Publish plugin
    // that appier module cannot determine which publication of the module
    // to depend on either debug or release.
    //
    // See https://stackoverflow.com/questions/51247830/publishing-is-not-able-to-resolve-a-dependency-on-a-project-with-multiple-public.
    //
    // In order to publish releases, you must invoke publish with -Dpublication option
    //
    // Examples:
    // - ./gradlew aiqua:publish -Dpublication=debug
    // - ./gradlew aiqua:publish -Dpublication=release
    def publication = System.getProperty('publication')

    publishing {
        publications {
            if (publication == 'release') {
                release(MavenPublication) {
                    from components.release
                    artifact sourcesJar {
                        classifier "sources" // this appends '-sources' to the filename of jar
                    }
                    artifact javadocJar {
                        classifier "javadoc" // this appends '-javadoc' to the filename of jar
                    }
                    groupId = rootProject.groupId
                    artifactId = project.artifactId
                    version = version
                    pom {
                        name = "${rootProject.groupId}:${project.artifactId}"
                        description = project.description
                        url = rootProject.url
                        scm {
                            url = rootProject.scmUrl
                        }
                        licenses {
                            license {
                                name = rootProject.licenseName
                                url = rootProject.licenseUrl
                            }
                        }
                        developers {
                            developer {
                                email = rootProject.developerEmail
                            }
                        }
                    }
                }
            }
            if (publication == 'debug') {
                debug(MavenPublication) {
                    from components.stagingDebug
                    artifact sourcesJar {
                        classifier "sources" // this appends '-sources' to the filename of jar
                    }
                    artifact javadocJar {
                        classifier "javadoc" // this appends '-javadoc' to the filename of jar
                    }
                    groupId = rootProject.groupId
                    artifactId = "${project.artifactId}${project.debugSuffix}"
                    version = version
                    pom {
                        name = "${rootProject.groupId}:${project.artifactId}${project.debugSuffix}"
                        description = project.description
                        url = rootProject.url
                        scm {
                            url = rootProject.scmUrl
                        }
                        licenses {
                            license {
                                name = rootProject.licenseName
                                url = rootProject.licenseUrl
                            }
                        }
                        developers {
                            developer {
                                email = rootProject.developerEmail
                            }
                        }
                    }
                }
            }
        }
    }
}

apply from: "$rootProject.projectDir/publish-maven-central.gradle"
